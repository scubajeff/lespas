plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId 'site.leos.apps.lespas'
        minSdk 25
        targetSdk 31
        versionCode 49
        versionName '2.5.2'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        archivesBaseName = "LesPas-v$versionName"
        resValue 'string', 'lespas_version', "v${versionName}"
        resValue 'string', 'sync_authority', "${applicationId}.StubProvider"
        resValue 'string', 'file_authority', "${applicationId}.fileProvider"
        resValue 'string', 'muzei_authority', "${applicationId}.muzei.ArtProvider"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true"
                ]
            }
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    aaptOptions {
        noCompress 'tflite'
    }
}

dependencies {
    // TensorFlow
    implementation 'org.tensorflow:tensorflow-lite-support:0.1.0'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.4.0'

    // Core
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'

    // Room components
    def roomVersion = "2.4.2"
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // Lifecycle components
    def lifecycleVersion = "2.4.1"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    // Kotlin components
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    def coroutineVersion = "1.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // Other UI components
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Media3
    def media3_version = "1.0.0-alpha01"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-datasource-okhttp:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"
    //implementation "androidx.media3:media3-session:$media3_version"

    // Chris Banes PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'

    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    //implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Muzei api
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.0'

    // OSMdroid and OSMbonuspack
    implementation('org.osmdroid:osmdroid-android:6.1.11')  {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    implementation('com.github.MKergall:osmbonuspack:6.7.0') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }

    // Authentication through system credentials
    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha04'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}