/*
 *   Copyright 2019 Jeffrey Liu (scubajeffrey@protonmail.ch)
 *
 *   Licensed under the Apache License, Version 2.0 (the "License");
 *   you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.devtools.ksp' version '2.2.0-2.0.2'
}

android {
    namespace 'site.leos.apps.lespas'
    compileSdk 36

    defaultConfig {
        minSdk 25
        targetSdk 35
        versionCode 108
        versionName '2.10.1'
        multiDexEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        archivesBaseName = "LesPas-v$versionName"
        resValue 'string', 'lespas_version', "v${versionName}"
        resValue 'string', 'sync_authority', "${namespace}.StubProvider"
        resValue 'string', 'file_authority', "${namespace}.fileProvider"
        resValue 'string', 'muzei_authority', "${namespace}.muzei.ArtProvider"

        ksp {
            arg('room.schemaLocation', "$projectDir/schemas".toString())
            arg('room.incremental', 'true')
        }
    }

    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            ext.enableCrashlytics = false
        }
        release {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    aaptOptions {
        noCompress 'tflite'
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    // TensorFlow
    def liteRTVersion = "1.4.0"
    implementation "com.google.ai.edge.litert:litert-support:$liteRTVersion"
    implementation "com.google.ai.edge.litert:litert:$liteRTVersion"
    implementation "com.google.ai.edge.litert:litert-gpu:$liteRTVersion"
    implementation "com.google.ai.edge.litert:litert-metadata:$liteRTVersion"

    // Core
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.9'

    // Room components
    def roomVersion = "2.7.2"
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    ksp "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // Lifecycle components
    def lifecycleVersion = "2.9.3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"

    // Kotlin components
    implementation 'androidx.work:work-runtime-ktx:2.10.3'
    def coroutineVersion = '1.10.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    // Other UI components
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.2.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.exifinterface:exifinterface:1.4.1'

    // Media3
    def media3_version = "1.8.0"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-datasource-okhttp:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"
    //implementation "androidx.media3:media3-session:$media3_version"

    // Chris Banes PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    //implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Muzei api
    implementation 'com.google.android.apps.muzei:muzei-api:3.4.2'

    // OSMdroid and OSMbonuspack
    implementation('org.osmdroid:osmdroid-android:6.1.20')  { exclude group: 'com.squareup.okhttp3', module: 'okhttp' }
    implementation('com.github.MKergall:osmbonuspack:6.9.0') { exclude group: 'com.squareup.okhttp3', module: 'okhttp' }

    // Authentication through system credentials
    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha05'

    // ZXing for blog QR code
    implementation 'com.google.zxing:zxingorg:3.5.0'

    // Jackson for faster json string processing
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.19.2'

    // Panorama support
    implementation('com.github.hannesa2:panoramagl:1.13') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        exclude group: 'androidx.core', module: 'core-ktx'
    }

    // Android TV theming support
    implementation 'androidx.leanback:leanback:1.2.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}